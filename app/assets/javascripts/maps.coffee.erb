$ -> window.render_map()

window.render_map = ->
  geodata_json = initialize_map('map').data('geojson')

  return unless geodata_json.length > 0

  marker_features = []
  walking_line_features = []
  other_line_features = []

  $.each geodata_json, ->
    marker_features.push {
      type: 'Feature'
      properties: title: @.title
      geometry: @
    } if @.type == 'Point'

    walking_line_features.push {
      type: 'Feature'
      geometry: @
    } if @.type == 'MultiLineString' && @.activity == 'wlk'

    other_line_features.push {
      type: 'Feature'
      geometry: @
    } if @.type == 'MultiLineString' && @.activity != 'wlk'

  map.on 'style.load', ->
    map.setPitch(40)
    map.fitBounds get_all_marker_coordinates(marker_features)
    add_lines_to_map('transit', other_line_features, '#999')
    add_lines_to_map('walking', walking_line_features, '#fff')
    add_markers_to_map('markers', marker_features)

    spin_animation_duration = 3000
    spin_animation_delay = 5000

    setTimeout ->
      map.rotateTo(80, { duration: spin_animation_duration })
    , spin_animation_delay
    setTimeout ->
      map.rotateTo(0, { duration: spin_animation_duration })
    , spin_animation_duration + spin_animation_delay

window.get_all_markers = ->
  markers = []
  $.each window.feature_layer.getLayers(), (index, layer) ->
    if layer.feature.geometry.type == 'Point'
      markers.push
        layer: layer
  markers

initialize_map = (identifier) ->
  mapboxgl.accessToken = '<%= ENV['MAPBOX_PUBLIC_TOKEN'] %>'
  window.map = new mapboxgl.Map
    container: identifier
    style: '<%= asset_path 'keepsakes_map_theme.json' %>'

  return $("##{identifier}")

add_markers_to_map = (id, marker_features) ->
  map.addSource id,
    type: 'geojson'
    data:
      type: 'FeatureCollection'
      features: marker_features

  map.addLayer
    id: id
    type: 'symbol'
    source: id
    layout: 'text-field': '{title}'
    paint: 'text-size': 16

add_lines_to_map = (id, line_features, color) ->
  map.addSource id,
    type: 'geojson'
    data:
      type: 'FeatureCollection'
      features: line_features

  map.addLayer
    id: id
    type: 'line'
    source: id
    layout:
      'line-join': 'round'
      'line-cap': 'round'
    paint:
      'line-color': color
      'line-width': 8

get_all_marker_coordinates = (marker_features) ->
  all_coordinates = []
  $.each marker_features, ->
    all_coordinates.push(this.geometry.coordinates.slice().reverse())

  all_coordinates
